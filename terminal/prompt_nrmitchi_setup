# Simple zsh prompt.
#
# dotfiles ❯ (default)
# dotfiles master ❯ (in git repository)
# root@serv dotfiles master ❯ (with SSH)
#
# * is prepended to git branch name if repo is dirty.
# ❯ is green or red depending on previous command exit status.
#
# Author: Paul Miller (nrmitchi.com)

vcs_info=''
kubectl_info=''

function prompt_nrmitchi_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
}

function list-files {
  if is-callable 'dircolors'; then
    ls --group-directories-first --color=auto .
  else
    ls -G
  fi
}

function get-vcs-info {
  vcs_info=''
  git rev-parse --is-inside-work-tree &>/dev/null || return

  local ref=$(git symbolic-ref -q HEAD | sed -e 's|^refs/heads/||')
  if [[ -z "$ref" ]]; then
    vcs_info=''
  else
    st=`git diff --quiet --ignore-submodules HEAD &>/dev/null; [ $? -eq 1 ] && echo '*'`
    vcs_info=" %F{blue}%F{magenta}${ref}${st}%f"
  fi
}

function get-kubectl-info {
  kubectl_info=''
  kubectl config current-context &>/dev/null || return

  local cluster=$(kubectl config current-context)

  if [[ -z "$cluster" ]]; then
    kubectl_info=''
  else
    kubectl_info=" %F{blue}%F{magenta}${cluster}%f"
  fi
}

function prompt_nrmitchi_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd get-vcs-info
  add-zsh-hook precmd get-kubectl-info
  add-zsh-hook chpwd list-files
  add-zsh-hook chpwd get-vcs-info
  add-zsh-hook chpwd get-kubectl-info

  # zstyle ':omz:module:editor' completing '%B%F{red}...%f%b'

  PROMPT='%F{yellow}%T %F{magenta}${SSH_TTY:+%n@%m }%F{cyan}%1~%f${vcs_info}\
%(!.%B%F{red}#%f%b.%B %(?.%F{green}.%F{red})❯%f%b) '
  RPROMPT='${kubectl_info}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_nrmitchi_setup "$@"

